apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'bintray-release'

group = 'com.koenv.ormlite-processor'
version = '0.2-SNAPSHOT'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.squareup:javapoet:1.0.0'
    compile 'com.google.guava:guava:18.0'

    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.26'
    testCompile 'com.google.testing.compile:compile-testing:0.6'
    testCompile files(org.gradle.internal.jvm.Jvm.current().getToolsJar())
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath 'com.novoda:bintray-release:0.2.10'
    }
}

publish {
    userOrg = 'koesie10'
    groupId = project.group
    artifactId = 'ormlite-processor'
    licences = ['MIT']
    publishVersion = project.version
    description = 'An OrmLite Annotation Processor for Android.'
    website = 'https://github.com/koesie10/ormlite-processor'
}

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(":generatePomFileForMavenPublication")) {
        taskGraph.getAllTasks().find{
            it.name == "generatePomFileForMavenPublication"
        }.doLast {
            file("build/publications/maven/pom-default.xml").delete()
            pom {
                //noinspection GroovyAssignabilityCheck
                project {
                    name project.name
                    description publish.description
                    url publish.website
                    version project.version

                    scm {
                        url 'git@github.com:koesie10/ormlite-processor.git'
                        connection 'scm:git:git@github.com:koesie10/ormlite-processor.git'
                        developerConnection 'scm:git:git@github.com:koesie10/ormlite-processor.git'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://www.opensource.org/licenses/mit-license.php'
                        }
                    }

                    developers {
                        developer {
                            id 'koesie10'
                            name 'Koen Vlaswinkel'
                            email 'koen@vlaswinkel.info'
                        }
                    }
                }
            }.writeTo("build/publications/maven/pom-default.xml")
        }
    }
}